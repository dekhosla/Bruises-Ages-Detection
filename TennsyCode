#include "c12880.h"

#define SPEC_TRG         A0 // ADC signal trigger to microcontroller
#define SPEC_ST          A1 // Sensor start pulse from microcontroller
#define SPEC_CLK         A2 // Sensor clock from microcontroller
#define SPEC_VIDEO       A3 // Sensor signal, buffered, low impedance to microcontroller
#define SPEC_EOS         A4   // Sensor End of Scan, not connected yet, to microcontroller
#define FLASH_TRIGGER    A5 // Light on/off from microcontroller

/* Defining the UART (serial1) as HWSERIAL*/

/*
ST Cycle minimum length is 381/f_clk
ST High period minimum 6/f_clk
ST Low perdiod minimum 375/f_clk 
ST Low is start configuration
ST High starts sequence
Integration starts at 4th clock cycle
ST Low
Integration finishes 51 cycles after ST switched to low
Integration time is ST high length plus 48 clks
Signal appears at 89th pulse and continues 288 pulses
*/

IntervalTimer flashTimer;

uint16_t spectrum[C12880_NUM_CHANNELS];

C12880 c12880(SPEC_TRG,SPEC_ST,SPEC_CLK,SPEC_VIDEO);

/******************************************************************************/
void setup(){
  
  pinMode(FLASH_TRIGGER,OUTPUT);
  digitalWrite(FLASH_TRIGGER,LOW);

  // Standard baud rates include 110, 300, 600, 1200, 2400, 4800, 9600, 14400, 19200, 38400, 57600, 115200, 128000 and 256000
  Serial1.begin(115200);
  //while (!Serial1 && millis() < 3000) ;
  Serial1.setTimeout(1);                              // Serial read timeout
  printHelp();

  memset((void*)spectrum, 0, sizeof(spectrum));
  c12880.begin();
 
}

//TODO CHANGE EVERY SERIAL TO SERIAL1

/******************************************************************************/
char   inBuff[] = "----------------";
int    bytesread;
String value    = "0.01";
String command  = "o";
float  integrationTime = 0.01;
float  triggerTime     = 0.01;
bool   continous = false;      //I changed this value to true so that the loop just runs.
bool   BINARY = false;

void loop(){
  if (Serial1.available()) {
     bytesread=Serial1.readBytesUntil('\n', inBuff, 16); // Read from serial until CR is read or timeout exceeded
     inBuff[bytesread]='\0';
     String instruction = String(inBuff);
     processInstruction(instruction);
  }
  if (continous) {
    c12880.read(spectrum);
    transmitSpectrum(spectrum);
  } else {
    delay(10);
  }
}

/******************************************************************************/
// SerialCommand handlers

void processInstruction(String instruction) {
    
   int instructionLength = instruction.length();
   if (instructionLength > 0) { command = instruction.substring(0,1); } 
   if (instructionLength > 1) {   value = instruction.substring(1,instructionLength); }
   //Serial1.println(command);
   //Serial.println(value);

   if (command =='I') { // set integration Time
     integrationTime = value.toFloat();
     if ((integrationTime < 0.0) || (integrationTime > 9999.99)) { Serial.println("Integration time out of valid Range"); }
     c12880.set_integration_time(integrationTime);
     Serial1.printf("Integratin time is: %g\n", integrationTime);
   } else if (command =='i') { // display integration time
     Serial1.printf("Integration time is: %g\n", integrationTime);
     
   } else if (command == "f") { // turn off flash
     Serial1.printf("Flash is off\n");
   } else if (command == "F") { // turn on flash
     int trig_micros = (int)(triggerTime*1e6);
     trig_micros = constrain(trig_micros,25,10000000);
     flashTimer.begin(flash_trigger, trig_micros);
     Serial1.println("Flash is on\n");
    
   } else if (command == "b") { // verbose
     BINARY = false;
     Serial1.printf("Verbose is on\n");
   } else if (command == "B") { // binary
     BINARY = true;
     Serial1.println("Binary is on\n");
      
   } else if (command == "r") { // readout once
     continous = false;   
     c12880.read(spectrum);
     transmitSpectrum(spectrum);
  
   } else if (command == "R") { // readout continous
     continous = true;
     
   } else { // invalid input, display status
     printHelp();
    
   } // end if else switch
}

void printHelp() {
  Serial1.println("C12880 Controller");
  Serial1.println("=================");
  Serial1.println("Set Intergation time:   I0.01");
  Serial1.println("Show Integration time:  i");
  Serial1.println("Use/Dont use flash:     F/f");
  Serial1.println("Use/Dont use binary:    B/b");
  Serial1.println("Read one spectrum:      r");
  Serial1.println("Read continous spectra: R");
}

void transmitSpectrum(uint16_t *data) {
  size_t i;
  if (BINARY) {
    for (i = 0; i < C12880_NUM_CHANNELS; i++) {
      Serial1.write( (byte *) &data[i], sizeof(data[1]));
    }
  } else {
    for (i = 0; i < C12880_NUM_CHANNELS; i++) { 
      Serial1.printf("%d, ",data[i]);
    }
  }
  Serial1.write('\n');
}

void flash_trigger(){
  digitalWrite(FLASH_TRIGGER,HIGH);
  flashTimer.end();
  digitalWrite(FLASH_TRIGGER,LOW);
}
